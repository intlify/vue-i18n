(*
 * Inltify message syntax v0.2
 * (vue-i18n compatible)
 *)

(* entrypoint *)
Resource ::= Plural | Mesasge;

(* main structure *)
Plural ::= Message Space* "|" Space* (Message (Space* "|" Space*)?)+;
Message ::= (Text? (Placeholder | Linked)? Text?)+;

(* primitives *)
Text ::= TextChar+;
Placeholder ::= Named | List | StringLiteral;
Modulo ::= "%";
Named ::= Modulo? "{" Space? (Identifier) Space? "}";
List ::= "{" Space? (NumberLiteral) Space? "}";
Linked ::= "@" (LinkedModifier)? LinkedDelimiter LinkedRefer;
LinkedRefer ::= LinkedKey | Placeholder;
LinkedKey ::= TextChar+;
LinkedModifier ::= LinkedDot Identifier;
LinkedDelimiter ::= ":";
LinkedDot ::= ".";

(* characters *)
AnyChar ::= [#x0-#xD7FF] | [#xE000-#xFFFD] | [#x10000-#x10FFFF];  (* Unicode character *)
SpecialChar ::= "{" | "}" | "|" | "@";
TextChar ::= AnyChar - SpecialChar - LineEnd;

(* literals *)
StringLiteral ::= "'" QuotedChar* "'";
NumberLiteral ::= "-"? Digits;
SpecialQuotedChar ::= "\'" | "\\";
SpecialEscape ::= "\\" SpecialQuotedChar;
UnicodeEscape ::= ("\\u" /[0-9a-fA-F]{4}/) | ("\\U" /[0-9a-fA-F]{6}/);
QuotedChar ::= (AnyChar - SpecialQuotedChar - LineEnd) | SpecialEscape | UnicodeEscape;

(* number *)
Digits ::= [0-9]+;

(* identifier *)
Identifier ::= [a-zA-Z] [a-zA-Z0-9_$]*;

(* whitespaces *)
SpaceInline ::= #x0020; (* "\u0020" *)
Tab ::= #x0009; (* \n0009 *)
LineEnd ::= #x000D#x000A | #x000A; (* "\u000D\u000A" | "\u000A" *)
Space ::= (Tab | SpaceInline | LineEnd)+;
