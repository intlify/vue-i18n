(*
 * Inltify message syntax v0.1
 * (vue-i18n compatible)
 *)

(* entrypoint *)
Resource ::= Plural | Mesasge;

(* main structure *)
Plural ::= Message Space* "|" Space* (Message (Space* "|" Space*)?)+;
Message ::= (Text? (Named | List | Linked)? Text?)+;

(* primitives *)
Text ::= TextChar+;
Named ::= "%"? "{" Space? (Identifier) Space? "}";
List ::= "{" Space? (Digits) Space? "}";
Linked ::= "@" LinkedModifier? ":" LinkedRefer;
LinkedRefer ::= "("? (LinkedKey | Named | List) ")"?;
LinkedModifier ::= "." LinkedArg;
LinkedKey ::= TextChar+;
LinkedArg ::= Identifier;

(* characters *)
AnyChar ::= [#x0-#xD7FF] | [#xE000-#xFFFD] | [#x10000-#x10FFFF];  (* Unicode character *)
SpecialChar ::= "{" | "}" | "|" | "@";
TextChar ::= AnyChar - SpecialChar - LineEnd;

(* number *)
Digits ::= [0-9]+;

(* identifier *)
Identifier ::= [a-zA-Z] [a-zA-Z0-9_$]*;

(* whitespaces *)
SpaceInline ::= #x0020; (* "\u0020" *)
Tab ::= #x0009; (* \n0009 *)
LineEnd ::= #x000D#x000A | #x000A; (* "\u000D\u000A" | "\u000A" *)
Space ::= (Tab | SpaceInline | LineEnd)+;
